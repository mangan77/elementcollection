allprojects {

    version = "1.0.0"
    project.ext.set("title", "elementcollection")
    project.ext.set("vendor", "mangan77")
    project.ext.set("vendorUrl", "https://github.com/mangan77/elementcollection")

    apply plugin: 'idea'

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 1.7

    repositories {
        mavenCentral();
    }

    jar {
        manifest {
            attributes(
                    "Manifest-Version": "1.0",
                    "Implementation-Title": project.title,
                    "Implementation-Version": version,
                    "Implementation-Vendor": vendor,
                    "Implementation-Url": vendorUrl,
                    "Build-Date": new Date().format('yyyy-MM-dd HH:mm:ss'),
                    "Build-Jdk": sourceCompatibility
            );
        }
        archiveName = rootProject.name + "-" + archiveName
        destinationDir = file("../releases/" + version)
    }

    test {
        useTestNG()
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
        archiveName = rootProject.name + "-" + archiveName
        destinationDir = file("../releases/" + version)
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
        archiveName = rootProject.name + "-" + archiveName
        destinationDir = file("../releases/" + version)
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

}


